; Tower of Hanoi 
; > Sample Input
;     > 2 a b c
; > Sample Output
;     > a->1->c
;     > a->2->b
;     > c->1->b

JMP main

; move from a to b with the help of c
@dfs ENT 0 ;(n a b c)
      ; n @ bp + 5
      ; a @ bp + 4
      ; b @ bp + 3
      ; c @ bp + 2
      ; jumpform @ bp + 1
      ; bptemp @ bp

  ; if n == 0 return
    LEA 5 LOD
    JNZ dfs_else LEV

  ; else
  @dfs_else
    ; dfs(n - 1, a, c, b)
    
    LEA 5 LOD PSH
      IMM 1
    SUB PSH
    LEA 4 LOD PSH
    LEA 2 LOD PSH
    LEA 3 LOD PSH
    CALL dfs
    ADJ 4
    
    ; output a->n->c
    LEA 4 LOD PUTC
    IMM 45 PUTC ;char '-'
    IMM 62 PUTC ;char '>'
    LEA 5 LOD WINT
    IMM 45 PUTC ;char '-'
    IMM 62 PUTC ;char '>'
    LEA 3 LOD PUTC
    IMM 10 PUTC ;char '\n'

    ; dfs(n - 1, c, b, a)
    LEA 5 LOD PSH
      IMM 1
    SUB PSH
    LEA 2 LOD PSH
    LEA 3 LOD PSH
    LEA 4 LOD PSH
    CALL dfs
    ADJ 4

    LEV


@main ENT 0
  RINT PSH
  GETC GETC PSH;
  GETC GETC PSH; 
  GETC GETC PSH;
  CALL dfs
  ADJ 4
  LEV

